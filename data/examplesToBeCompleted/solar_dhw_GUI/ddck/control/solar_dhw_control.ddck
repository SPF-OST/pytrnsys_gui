***************************************************
**Solar controller
**************************************************

*****************************************
** Contact person : Jeremias Schmidli   
** Creation date  : 28.04.2020 
** Last changes   : 22.06.2020, JS, change tSetAux to 50 and TdhwSet to 45 
*****************************************

***************************************************************************
** Description: 
** custom controlls for the solar dhw example system 
** TODO:
***************************************************************************

***********************************
** Sizing constants - just define here (overriden from config file)
***********************************
CONSTANTS 3
CollAM2PerMWh = 1
DhwVM3PerM2 = 1
QDemandMWh = 1

***********************************
** Control Temperatures
***********************************

EQUATIONS 6
myTCollOut = TCollOut          !@dependencyDdck
mySenTesSolarTop = Tsen9_Tes1 !@dependencyDdck
mySenTesSolarBot = Tsen1_Tes1 !@dependencyDdck
mySenTesAux = Tsen8_Tes1 !@dependencyDdck
tSetAux = 50
TdhwSet = TdhwSetProfile

***********************************
** Begin TYPE AUXILIAR CONTROLLER
***********************************

CONSTANTS 3
dtAuxOn = -3     ! Upper dead band coll. pump ON
dtAuxOff = -7     ! Lower dead band coll. pump OFF
sizeAux = 1

UNIT 27 TYPE 2  ! Tset - UDB < Tmonitored < Tset - LDB
PARAMETERS 2     
5     ! 1 No. of oscillations
98   ! 2 High limit cut-out
INPUTS 6     
tSetAux        ! Setpoint temperature, Tset
mySenTesAux ! Temperature to watch, Tmonitored
mySenTesAux ! High limit monitoring temperature
auxOn          ! 70,1 Input control function
dtAuxOn        ! Turn on temperature difference, Upper dead band, UDB dT
dtAuxOff       ! Turn off temperature difference, Lower dead band dT, LDB
0.0 20.0 0.0 0 dtAuxOn dtAuxOff      
EQUATIONS 1  
auxOn = [27,1]

CONSTANTS 1
DhwPAux_kW = 5

***********************************
** Begin TYPE COLLECTOR CONTROLLER
***********************************

CONSTANTS 2
dtColOn = 7    ! Upper dead band coll. pump ON, JS: was 10 before, yield is higher with 7, to be improved
dtColOff = 3    ! Lower dead band coll. pump OFF


UNIT 28 TYPE 2  
PARAMETERS 2     
5     ! 1 No. of oscillations
98   ! 2 High limit cut-out
INPUTS 6     
myTCollOut     ! Upper input temperature Th
mySenTesSolarBot ! Lower input temperature Tl
mySenTesSolarTop ! Monitoring temperature Tin
puColOn        ! 70,1 Input control function
dtColOn        ! Upper dead band dT
dtColOff       ! Lower dead band dT
0.0 20.0 0.0 0 dtColOn dtColOff      
EQUATIONS 1  
puColOn = [28,1]*LT(myTcollOut,120)


EQUATIONS 4
*** Massflowrates
MfrPuCol = puColOnRc*MfrCPriNom
MfrWTap = -MfrDHWset*0.125
MfrWTap2 = -MfrDHWset*0.875
MfrWTap_main = MfrDHWset

***********************************
** Begin TYPE Recall
***********************************

UNIT 18 TYPE 993 
PARAMETERS 1     
2   ! 1: number of variables to be remembered    
INPUTS 2     
auxOn puColOn     
0.0  0.0  
EQUATIONS 2    ! outputs of Input Value Recall
auxOnRc = [18,1]      
puColOnRc = [18,2]  


*************
**END Solar controller
************




